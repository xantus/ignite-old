#!/usr/bin/perl

use File::Basename 'dirname';
use File::Spec;

my $bin;
BEGIN {
    $bin = join( '/', File::Spec->splitdir(dirname(__FILE__)) );
}

use lib "$bin/lib";
use lib "$bin/../lib";

use Mojolicious::Lite;
use Ignite::Lite;

app->static->root( "$bin/../public" );
app->log->level( 'debug' );

@ARGV = qw( daemon ) unless @ARGV;

# to install (change secret to some random text):
# curl -X PUT http://127.0.0.1:5984/ignite
# curl -X PUT http://127.0.0.1:5984/ignite/config -d '{ "base": "/socket.io", "mojo_config": { "secret": "CHANGE THIS" } }'

# the config will be auto created for you, if the config key or db isn't found
# to edit your config visit: http://127.0.0.1:5984/_utils/document.html?ignite/config
ignite 'config' => 'http://127.0.0.1:5984/ignite'; # http://127.0.0.1:5984/ignite/$id will also be accepted

# list of last 15 msgs
my $msgs = [];

socketio 'open' => sub {
    my ( $client ) = @_;

    $client->send_message({ sessionid => $client->id });
    $client->send_message({ buffer => $msgs });

    $client->broadcast({ announcement => $client->id . ' connected' });

    $client->heartbeat( 10 );
};

socketio 'close' => sub {
    my ( $client ) = @_;

    $client->broadcast({ announcement => $client->id . ' disconnected' });
    $client->disconnect();
};

socketio 'message' => sub {
    my ( $client, $data ) = @_;

    my $msg = { message => [ $client->id, $data ] };
    push( @$msgs, $msg );
    shift @$msgs if ( $#{$msgs} > 15 );

    $client->broadcast($msg);
};

app->start;

1;
