#!/usr/bin/perl

use File::Basename 'dirname';
use File::Spec;

my $bin;
BEGIN {
    $bin = join( '/', File::Spec->splitdir(dirname(__FILE__)) );
}

use lib "$bin/lib";
use lib "$bin/../lib";

use Mojolicious::Lite;
use Ignite::Lite;

# serve static files from here
app->static->root( "$bin/../public" ); # if ran from examples

# noisy log
app->log->level( 'debug' );

# auto daemon
@ARGV = qw( daemon ) unless @ARGV;

# the config will be auto created for you, if the config key or db isn't found
# to edit your config visit: http://127.0.0.1:5984/_utils/document.html?ignite/config
ignite->init( 'http://127.0.0.1:5984/ignite/config' );

# list of last 15 msgs
my $msgs = [];

# connection from browser to ignite opened
socketio 'open' => sub {
    my ( $client ) = @_;

    $client->send_message({ sessionid => $client->id });
    $client->send_message({ buffer => $msgs });

    $client->broadcast({ announcement => $client->id . ' connected' });

    $client->heartbeat( 10 );
};

# connection from browser to ignite closed
socketio 'close' => sub {
    my ( $client ) = @_;

    $client->broadcast({ announcement => $client->id . ' disconnected' });
    $client->disconnect();
};

# message sent from browser to ignite
socketio 'message' => sub {
    my ( $client, $data ) = @_;

    my $msg = { message => [ $client->id, $data ] };
    push( @$msgs, $msg );
    shift @$msgs if ( $#{$msgs} > 15 );

    $client->broadcast($msg);
};

print "go to http://localhost:3000/examples/chat.html\n";
app->start;

