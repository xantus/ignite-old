#!/usr/bin/env perl

# Copyright (C) 2008-2010, Sebastian Riedel.

use strict;
use warnings;

# After all this time, somebody else with one eye... who ISN'T a clumsy
# carpenter or a kid with a BB gun.
use Mojo::IOLoop;

# Run as root only
die "Server needs to run as user root to be able to listen to port 843.\n"
    unless $> == 0 && $< == 0;

# The loop
my $loop = Mojo::IOLoop->new;

# Flash policy xml
my $xml = <<'EOF';
<?xml version="1.0"?>
<!DOCTYPE cross-domain-policy SYSTEM "/xml/dtds/cross-domain-policy.dtd">
<cross-domain-policy>
<site-control permitted-cross-domain-policies="master-only"/>
<allow-access-from domain="*" to-ports="*" secure="false"/>
</cross-domain-policy>
EOF

# Flash policy server example
$loop->listen(
    port => 843,
    cb   => sub {
        my ($loop, $id) = @_;

        # Read callback
        $loop->read_cb(
            $id => sub {
                my ($loop, $id, $chunk) = @_;

                # Writing
                $loop->writing($id);
            }
        );

        # Write callback
        $loop->write_cb(
            $id => sub {
                my ($loop, $id) = @_;

                # Finish
                $loop->drop($id);

                # Write XML
                return $xml;
            }
        );

        # Reading only
        $loop->not_writing($id);
    }
) or die "Couldn't create listen socket!\n";

print "Starting server on port 843.\n";

# Start loop
$loop->start;

1;
